* TI calculation for solute in gas phase - ele - pc 
* Mike DEVEREUX (2017), Kai TOEPFER (2022)
* 

PRNLEV 2 NODE 0
BOMLEV -7

!======================================
! Read Topology and Parameter Files
!======================================

! Modified topology with scaled atomic charges
READ RTF CARD NAME -
 {FTOP}

READ PARAM CARD NAME -
 {FPAR}

!======================================
! Generate Solute and Set Positions
!======================================

OPEN UNIT 10 READ CARD NAME {FPDB_SOLU}
READ SEQUENCE PDB UNIT 10
GENERATE SOLU
REWIND UNIT 10
READ COOR PDB UNIT 10
CLOSE UNIT 10

OPEN UNIT 10 READ CARD NAME {FPDB_SOLV}
READ SEQUENCE PDB UNIT 10
GENERATE SOLV
REWIND UNIT 10
READ COOR PDB UNIT 10
CLOSE UNIT 10

OPEN WRITE UNIT 10 CARD NAME system.psf
WRITE PSF UNIT 10 CARD

!======================================
! Custom Additions
!======================================

! Constrain bonds involving hydrogen atoms
SHAKE BONH TOL 1.0E-8 PARA

! RKHS
!TKRN ACTI rkhs_scn_drz 0 0 16 jdrz -
!  hartree bohr -
!  sele resname SCN .and. type N3C end -
!  sele resname SCN .and. type C3N end -
!  sele resname SCN .and. type S end

! Scale Rmin+%
SCALAR RSCA SET 1.00 SELE SEGI SOLU END

!======================================
! Set Simulation Parameters
!======================================

! Electrostatic scheme (just point charges if empty)
{ELST}

! Modify Solute-Solvent interaction by lambda value
SET LAMBDA {VLAM}
BLOCK 2
  CALL 2 SELE SEGI SOLU END
  COEFF 1 2 1.0 ELEC @LAMBDA
END
!SCALAR CHARGE MULT @LAMBDA SELE SEGI SOLU END

! Set non-bonded parameters
NBONDS NBXMOD 5 ATOM CDIEL EPS 1.0 SHIFT VATOM VDISTANCE -
  VSWITCH CUTNB 99.0 CTOFNB 98.0 CTONNB 97. E14FAC 1.0

! Set periodic boundary conditions
COOR STAT
CALC BOXX = (?XMAX - ?XMIN -1.0)
CRYSTAL DEFI CUBIC @BOXX @BOXX @BOXX 90. 90. 90.
CRYSTAL BUILD nope 0
IMAGE BYRES XCEN 0.0 YCEN 0.0 ZCEN 0.0 SELE ALL END

! Update non-bonded parameters
NBONDS - !EWALD PMEWALD KAPPA 0.32 -
  CUTNB 14.0 CTOFNB 12.0 CTONnb 10.0 CUTIM 14.0 -
  ATOM VATOM VDISTANCE -
  VSWITCH SHIFT CDIE EPS 1.0 E14FAC 1.0 WMIN 1.5

! CPT Parameter
SCALAR MASS STAT
CALC PMASS = INT ( ?STOT  /  50.0 )
CALC TMASS = @PMASS * 10
  
!======================================
! Run Simulation
!======================================

! Read restart file
OPEN UNIT 31 READ CARD NAME {FRES}
READ COOR DYNR CURR UNIT 31

! Equilibration run
OPEN UNIT 32 WRITE CARD NAME equi.res

DYNA LEAP RESTART - 
  NSTEP {NEQUI} TIMESTEP {TSTEP} -
  NTRFRQ 100 IPRFRQ 1000 INBFRQ -1 IMGFRQ 250 -
  ECHECK -1 -
  - !TBATH {TBATH} FIRSTT {TFRST} FINALT {TBATH} -
  CPT PCONst PREF 1.0 PGAMMA 0 PMASS @PMASS -
  HOOVER REFT {TBATH} TMASS @TMASS -
  NPRINT 1000 NSAVC -1 IUNREAD 31 IUNWRITE 32
  
! Production run
OPEN UNIT 31 READ CARD NAME equi.res
OPEN UNIT 32 WRITE CARD NAME prod.res
OPEN UNIT 33 WRITE NAME prod.dcd

DYNA LEAP RESTART - 
  NSTEP {NPROD} TIMESTEP {TSTEP} -
  NTRFRQ 100 IPRFRQ 1000 INBFRQ -1 IMGFRQ 250 -
  ECHECK -1 -
  CPT PCONst PREF 1.0 PGAMMA 0 PMASS @PMASS -
  HOOVER REFT {TBATH} TMASS @TMASS -
  NPRINT 1000 NSAVC {NSAVE} IUNREAD 31 IUNWRITE 32 IUNCRD 33

CLOSE UNIT 31
CLOSE UNIT 32
CLOSE UNIT 33

!======================================
! Scan trajectory
!======================================

! Reset Solute-Solvent interaction
BLOCK 2
  COEFF 1 2 1.0 ELEC 1.0
  COEFF 1 1 1.0 ELEC 0.0
END
!SCALAR CHARGE DIVI @LAMBDA SELE SEGI SOLU END

! Open production trajectory file
OPEN UNIT 33 READ NAME prod.dcd

! Open result file
OPEN WRITE CARD UNIT 34 NAME {FDAT}
WRITE TITLE UNIT 34
*  Electrostatics Solute
*  Time, Electrostatics (central), Electrostatics (image)
*

! Fix SOLV atoms (energy contribution zero)
CONS FIX SELE .NOT. SEGI SOLU END

! Define trajectory
TRAJECTORY FIRSTUNIT 33 NUNIT 6 SKIP 1

! Loop over trajectory
SET ITRAJ 0
LABEL SNAP
  TRAJ READ
  ENERGY
  SET TIME ?TIME
  SET TELE ?ELEC
  SET TIEL ?IMEL
  WRITE TITLE UNIT 34
* @TIME @TELE @TIEL
*
  INCR ITRAJ BY 1
IF @ITRAJ .LT. {NTRAJ} GOTO SNAP

CLOSE UNIT 33
CLOSE UNIT 34

STOP
