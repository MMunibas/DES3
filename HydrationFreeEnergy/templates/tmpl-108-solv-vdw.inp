* TI calculation for solution-phase - vdW 
* Mike DEVEREUX (2017), Kai TOEPFER (2022)
* 

PRNLEV 2 NODE 0
BOMLEV -7

!======================================
! Read Topology and Parameter Files
!======================================

READ RTF CARD NAME -
 {FTOP}

READ PARAM CARD NAME -
 {FPAR}

!======================================
! Generate Solute and Set Positions
!======================================

OPEN UNIT 10 READ CARD NAME {FPDB_SOLU}
READ SEQUENCE PDB UNIT 10
GENERATE SOLU
REWIND UNIT 10
READ COOR PDB UNIT 10
CLOSE UNIT 10

OPEN UNIT 10 READ CARD NAME {FPDB_SOLV}
READ SEQUENCE PDB UNIT 10
GENERATE SOLV
REWIND UNIT 10
READ COOR PDB UNIT 10
CLOSE UNIT 10

OPEN WRITE UNIT 10 CARD NAME system.psf
WRITE PSF UNIT 10 CARD

!======================================
! Custom Additions
!======================================

! Constrain bonds involving hydrogen atoms
SHAKE BONH TOL 1.0E-8 PARA

! RKHS
!TKRN ACTI rkhs_scn_drz 0 0 16 jdrz -
!  hartree bohr -
!  sele resname SCN .and. type N3C end -
!  sele resname SCN .and. type C3N end -
!  sele resname SCN .and. type S end

! Scale Rmin+%
SCALAR RSCA SET 1.08 SELE SEGI SOLU END

!======================================
! Set Simulation Parameters
!======================================

! Set non-bonded parameters
NBONDS NBXMOD 5 ATOM CDIEL EPS 1.0 SHIFT VATOM VDISTANCE -
  VSWITCH CUTNB 99.0 CTOFNB 98.0 CTONNB 97. E14FAC 1.0

FAST OFF  ! important, otherwise scalar RSCA has no effect!

! Set electrostatic interaction between solute-solvent to zero
!BLOCK 2
!  CALL 2 SELE SEGI SOLU END
!  COEFF 1 2 1.0 ELEC 0.0
!END
SCALAR CHARGE SET 0. SELE SEGI SOLU END

! Initialize PERTubation module
PERT SELE SEGI SOLU END

! Set vdW-radii factor to zero -
! Shouldn't be that command before PERT to declare lambda=0 state?
SCALAR RSCA SET 0. SELE SEGI SOLU END

! Set periodic boundary conditions
COOR STAT
CALC BOXX = (?XMAX - ?XMIN -1.0)
CRYSTAL DEFI CUBIC @BOXX @BOXX @BOXX 90. 90. 90.
CRYSTAL BUILD nope 0
IMAGE BYRES XCEN 0.0 YCEN 0.0 ZCEN 0.0 SELE ALL END

! Update non-bonded parameters
NBONDS ATOM - !EWALD PMEWALD KAPPA 0.32 -
CUTNB 14.0 CTOFNB 12.0 CTONnb 10.0 CUTIM 14.0 -
ATOM VATOM VDISTANCE -
VSWITCH SHIFT CDIE EPS 1.0 E14FAC 1.0 WMIN 1.5

! CPT Parameter
SCALAR MASS STAT
CALC PMASS = INT ( ?STOT  /  50.0 )
CALC TMASS = @PMASS * 10

!======================================
! Run Simulation
!======================================

OPEN READ UNIT 32 CARD NAME {FRES}
READ COOR DYNR CURR UNIT 32
bomlev 0

! Production run
OPEN UNIT 33 WRITE NAME prod.dcd

! Start NPT simulation
DYNA LEAP RESTART -
  NSTEP {NBOTH} TIMESTEP {TSTEP} -
  NTRFRQ 100 IPRFRQ 1000 INBFRQ -1 IMGFRQ 250 -
  IHTFRQ 0 IEQFRQ 0 -
  - !TBATH {TBATH} TSTRUCT {TBATH} FINALT {TBATH} FIRSTT {TFRST} -
  CPT PCONst PREF 1.0 PGAMMA 0 PMASS @PMASS -
  HOOVER REFT {TBATH} TMASS @TMASS -
  NPRINT 1000 NSAVC -1 IUNREAD 32 IUNWRI -1 IUNVEL -1 KUNIT -1 IUNCRD 33 -
  {OPERT}

OPEN WRITE CARD UNIT 34 NAME {FDAT}
WRITE TITLE UNIT 34
*  vdW Solute
*  Slow Growth total energy, fluctuation
*

SET EPRTOT ?SLTOT
SET DIFFLC ?DFLC

WRITE TITLE UNIT 34
* @EPRTOT @DIFFLC
*

CLOSE UNIT 34

STOP

