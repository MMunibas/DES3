* Based in input of Maryam and Luis
* Heating and Equilibrium Run
*

bomlev -1
prnlev 5
wrnlev 1

!#########################################
! Tasks
!#########################################

! 0:    Do it, or
! Else: Do not, there is no try!
set mini 0
set heat 0
set equi 0

! Start Production at dcd number n
set ndcd 0

!#########################################
! Setup System
!#########################################

! Read topology and parameter files
stream toppar.str

! Read PSF
open read unit 10 card name step1.psf
read psf  unit 10 card xplor

!Read Coordinate
open read unit 10 card name step1.crd
read coor unit 10 card

!#########################################
! Setup PBC (Periodic Boundary Condition)
!#########################################

coor stat sele all end

calc xdim = int ( ( ?xmax - ?xmin + 0.0 ) ) + 1
calc ydim = int ( ( ?ymax - ?ymin + 0.0 ) ) + 1
calc zdim = int ( ( ?zmax - ?zmin + 0.0 ) ) + 1

set bsiz = 0

if @xdim .gt. @bsiz then
   set bsiz = @xdim
endif
if @ydim .gt. @bsiz then
   set bsiz = @ydim
endif
if @zdim .gt. @bsiz then
   set bsiz = @zdim
endif

open read unit 10 card name crystal_image.str
crystal defi cubic @bsiz @bsiz @bsiz 90. 90. 90.
crystal build nope 0
image byres xcen 0.0 ycen 0.0 zcen 0.0 sele all end

!#########################################
! Bonded/Non-bonded Options & Constraints
!#########################################

! Electrostatic FDCM model
OPEN UNIT 40 CARD READ NAME scn_fluc.dcm
DCM FLUX 99 IUDCM 40 TSHIFT
CLOSE UNIT 40

! Non-bonding parameters
nbonds atom ewald pmewald kappa 0.43  -
  fftx 32 ffty 32 fftz 32 order 4 -
  cutnb 14.0  ctofnb 12.0 ctonnb 10.0 -
  lrc vdw vswitch -
  inbfrq -1 imgfrq -1

! Constrains
shake bonh para sele all end

!#########################################
! Minimization
!#########################################

if @mini .eq. 0 then

set n 1
label loop_edit

  ic edit
  dist @n N3C @n C3N 1.192
  dist @n C3N @n S 1.689
  angle @n N3C @n C3N @n S 180.0
  end

increase n by 1
if n le FFF goto loop_edit

ic print

const ic bond 2000.0 angle 2000.0

mini abnr nstep 1000 nprint 100

ic delete bond first sele type N3C end
ic delete bond first sele type C3N end
ic delete angle first sele type N3C end
const ic

mini sd nstep 1000 nprint 100

open write unit 10 card name mini.pdb
write coor unit 10 pdb

endif

!#########################################
! Heating
!#########################################

scalar mass stat
calc pmass = int ( ?stot  /  50.0 )
calc tmass = @pmass * 10

calc tmin = XXX * 0.2 

if @heat .eq. 0 then

open write unit 31 card name heat.res       ! Restart file
open write unit 32 file name heat.dcd       ! Coordinates file

dyna leap verlet start -
   timestp 0.001 nstep TTT1 -
   firstt @tmin finalt XXX tbath XXX -
   ihtfrq 1000 teminc 5 ieqfrq 0 -
   iasors 1 iasvel 1 iscvel 0 ichecw 0 -
   nprint 1000 nsavc 1000 ntrfrq 200 -
   iseed  RRRHH1 RRRHH2 RRRHH3 RRRHH4 -
   echeck 100.0   -
   iunrea -1 iunwri 31 iuncrd 32 iunvel -1

open unit 1 write card name prepare.crd
write coor card unit 1
close unit 1

open write unit 10 card name prepare.pdb
write coor unit 10 pdb

endif

!#########################################
! Equilibration
!#########################################

if @equi .eq. 0 then

open read  unit 30 card name heat.res      ! Restart file
open write unit 31 card name equi.res      ! Restart file
open write unit 32 file name equi.dcd      ! Coordinates file

dyna restart leap cpt nstep TTT2 timestp 0.001 -
  nprint 1000 nsavc 1000 ntrfrq 200 -
  iprfrq 500 inbfrq 10 imgfrq 50 ixtfrq 1000 -
  ihtfrq 0 ieqfrq 0 echeck -1 -
  pint pconst pref 1 pgamma 5 pmass @pmass -
  hoover reft XXX tmass @tmass firstt XXX -
  iunrea 30 iunwri 31 iuncrd 32 iunvel -1


open unit 1 write card name equi.crd
write coor card unit 1
close unit 1

open write unit 10 card name equi.pdb
write coor unit 10 pdb

close unit 30
close unit 31
close unit 32

endif

!#########################################
! Production
!#########################################

label loop

if @ndcd .eq. 0 then
  set m @ndcd
  open read unit 33 card name equi.res        ! Restart file
  open write unit 34 card name dyna.@ndcd.res ! Restart file
  open write unit 35 file name dyna.@ndcd.dcd ! Coordinates file
else
  calc m @ndcd-1
  open read unit 33 card name dyna.@m.res
  open write unit 34 card name dyna.@ndcd.res
  open write unit 35 file name dyna.@ndcd.dcd
endif

dyna restart leap res nstep TTT3 timestp SSS -
  nprint 100 nsavc NSV ntrfrq 200 -
  iprfrq 1000 inbfrq -1 imgfrq 50 ixtfrq 1000 -
  ihtfrq 0 ieqfrq 0 -
  cpt pint pconst pref 1 pgamma 0 pmass @pmass -
  hoover reft XXX tmass @tmass -
  IUNREA 33 IUNWRI 34 IUNCRD 35 IUNVEL -1
  
open unit 1 write card name dyna.@ndcd.crd
write coor card unit 1
close unit 1

open write unit 10 card name dyna.@ndcd.pdb
write coor unit 10 pdb

close unit 33
close unit 34
close unit 35

increase ndcd by 1
if ndcd lt NNN goto loop

STOP
